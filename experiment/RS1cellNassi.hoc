//Tempalte for layer V prefrontal pyramidal neurons
zindex=0


func num2ind(){ // a mapping from numbers to morphology file indices
    return ($1 % 56)
}

proc current_balance() {
	  finitialize($1)
	  fcurrent()	
	  printf("Balancing each compartment to %d mV\n", $1)
	  forall {
	    	for (x) {
	    		if (ismembrane("na_ion")) {e_pas(x)=v(x)+ina(x)/g_pas(x)} 
	   		if (ismembrane("k_ion")) {e_pas(x)=e_pas(x)+ik(x)/g_pas(x)} 
	    		if (ismembrane("ca_ion")) {e_pas(x)=e_pas(x)+ica(x)/g_pas(x)}
	      	        if (ismembrane("in_ion")) {e_pas(x)=e_pas(x)+in(x)/g_pas(x)} 
	       		if (ismembrane("h")) {e_pas(x)=e_pas(x)+ihi(x)/g_pas(x)}
	      		fcurrent()
	   		}
		}
	finitialize(v_init)
	fcurrent()
	frecord_init()
}



begintemplate Pcell

external current_balance

public all, somatic, axonal, basal, apical, soma, axon, dend, apic
//create soma[1], axon[1], dend[18], apic[25]
//create soma[1], axon[7], dend[19], apic[11]
create soma[1], axon[7], dend[26], apic[13]


objref all, somatic, axonal, basal, apical

proc subsets() { local i
	objref all,somatic, axonal, basal, apical 
  	all = new SectionList()
    	soma all.append()
    	for i=0, 25 {dend[i] all.append()}
    	for i=0, 12 {apic[i] all.append()}

  	somatic = new SectionList()

    	soma somatic.append() //was [0]

  	axonal = new SectionList()
    	axon axonal.append() //was [0]

  	basal = new SectionList()
    	for i=0, 25 {dend[i] basal.append()}

 	apical = new SectionList()
    	for i=0, 12 {apic[i] apical.append()}
}

proc cut_sections() {  
	forall {
      		nseg=1+int(L/$1)
}}

proc Rm_sigmoid_basal() { local rm
	Rm_soma = Rm_default
	Rm_end=  Rm_default*0.5
	Rm_dhalf=10 
	Rm_steep=5
     	for (x) {  
		xdist = distance(x)        
       		rm = Rm_soma + (Rm_end - Rm_soma)/(1.0 + exp((Rm_dhalf-xdist)/Rm_steep))
       		g_pas(x) = 1.0/rm
}}

proc Rm_sigmoid_apical() { local rm
	Rm_soma = Rm_default
	Rm_end=  Rm_default*0.5
	Rm_dhalf=300 
	Rm_steep=50
	for (x) {  
       		xdist = distance(x)    // calc. path distance  
	       	rm = Rm_soma + (Rm_end - Rm_soma)/(1.0 + exp((Rm_dhalf-xdist)/Rm_steep))
       		g_pas(x) = 1.0/rm
}}

proc Ra_sigmoid() {  
	Ra_soma=Ra_default
	Ra_end=Ra_default*10
	dhalf=10
	steep=5
     	for (x) {  
       		xdist = distance()  //calc. perpedicular distance
       		Ra = Ra_soma + (Ra_end - Ra_soma)/(1.0 + exp((dhalf-xdist)/steep))
}}

max_ar2=0
min_ar2=0
decay_start=0   
decay_end=0

strdef ar24_tmp_str
objref  strobj, ar24_f
strobj=new StringFunctions()
ar2_firsttime=1

proc ar2_log() {
  	if (!ar2_firsttime) { return }
  	//ar24_f=new File() 
  	//sprint($o3.tmp_str3, "%s/ar2_log", $o3.generic_dir)
  	//ar24_f.wopen($o3.tmp_str3)
  	//ar24_f.printf("%s:",$s1)
  	while (strobj.substr($s2, "*") > -1) {
      		index=strobj.head($s2, "\\*", ar24_tmp_str)
         	strobj.right($s2, 1+index)
          	$o3.create_variable("ar24_val", ar24_tmp_str)
      		ar24_f.printf("%s:%g:", ar24_tmp_str, ar24_val)
     	}
  	$o3.create_variable("ar24_val", $s2)
  	ar24_f.printf("%s:%g\n", $s2, ar24_val)
    	ar24_f.close()
  	ar2_firsttime=0
}

proc endpt() {
  	P=(n3d()-1)*$1
  	x_d3($1)=x3d(P)
  	y_d3($1)=y3d(P)
  	z_d3($1)=z3d(P)
}

proc fracpt() { local posn, A
  	A=$1
  	posn=$2
  	x_d3(posn)=x3d(i-1) + (x3d(i) - x3d(i-1))*A
  	y_d3(posn)=y3d(i-1) + (y3d(i) - y3d(i-1))*A
  	z_d3(posn)=z3d(i-1) + (z3d(i) - z3d(i-1))*A
}

proc map_segments_to_3d() {
    	forall {
	    	insert d3
	    	i=0
	    	endpt(0)
	    	for (x) if (x > 0 && x < 1) {
	      		while (arc3d(i)/L < x) {
	        		i += 1
	      		}
	      		D=arc3d(i) - arc3d(i-1)
	      		if (D <= 0) {
	      			printf("\t\t * %s had a D < 0\n", secname())
	      		}
	      		alpha = (x*L - arc3d(i-1))/D
	      		fracpt(alpha,x)
	    	}
	    	endpt(1)
}}
/// current_balance was here..//

proc init() {
	//create soma[1], axon[1], dend[18], apic[25]
	zindex=$1*100
	//xopen("../../morphology/ratpfc22.hoc")
	//xopen("../../morphology/smith_new_axon/0-2a_axon.hoc")
	xopen("../../morphology/smith_new_axon/0-2_axon.hoc")
	subsets()

	Rm_default=30000 
	Rm_dend=15000//30000*0.5    
	Ra_default= 100			
	Cm_default = 1.2	
	Cm_dend = 2.0//1.0*2
	v_init = -66	
	celsius=34

	cut_sections(75)
	map_segments_to_3d()
	
	// Set HH Sodium - Potassium propertiess
	gna_default = 0.031 	
	gkdrbar_default=0.045  
	soma_caN = 0.3e-4	
	soma_caL = 3e-4  
	soma_car = 3e-5*25  //*0.5//75e-5//*0.5 	//MODIFIED FOR SUSTAINED
	soma_caT = 1e-5*10//1e-4 //MODIFIED FOR SUSTAINED	    
	soma_kca =  0.005*5*0.5 //0.025*0.5  
	mykca_init =0.006*5*0.001   // * 0.001 //STEFANOS EDIT    //0.00003    
	kad_init = 0.00075*1.2  //0.0009	
	soma_kdBG = 0.0012*4.4
	soma_hbar = 1.872e-5*0.5 //1.872e-5*0.5    		
	soma_nap = 1.2e-5   //*0.5 //MODIFIED FOR SUSTAINED
	gCAN = 0.0001 	

	//===================================================================================== 
	forsec somatic {
	       	nseg = 21
		insert pas   
	        g_pas 	= 1/Rm_default
	        e_pas 	= v_init
	       	Ra    	= Ra_default
		cm 	= Cm_default

		insert Naf
		gnafbar_Naf = gna_default*5  
			
		insert kdr
		gkdrbar_kdr = gkdrbar_default
		
	        insert h     
	        gbar_h  = soma_hbar
	        K_h     = 8.8
	        vhalf_h = -82

		insert kad  
	      	gkabar_kad = kad_init
	                  
	       	insert cal 
		gcalbar_cal=soma_caL

	       	insert can
		gcalbar_can=soma_caN   

	       	insert kca
		gbar_kca = soma_kca

	      	insert mykca 
		gkbar_mykca = mykca_init

	      	insert cad

	   	insert Ks
		gKsbar_Ks = soma_kdBG
			    
	       	insert cat 
	      	gcatbar_cat = soma_caT   

	   	insert car
		gcabar_car=soma_car

	    	insert nap	
		gnabar_nap=soma_nap*0.1
	 
	    	insert ican
	        gbar_ican  = gCAN       
		}

	forsec axonal {
		nseg=5
		insert pas  
	       	g_pas       = 1/Rm_default
	       	e_pas       = v_init
	       	Ra          = Ra_default
	       	cm          = Cm_default

		insert Naf
		gnafbar_Naf = gna_default*10
			
		insert kdr
		gkdrbar_kdr = gkdrbar_default  
	}

	access soma//was [0]
	distance()

	forsec apical {	
		insert pas
		e_pas          = v_init                    
	 	Ra             = Ra_default*0.5	
	     	cm             = 1.2  
		Rm_sigmoid_apical()	 //without argument?!?	
		
		insert Naf
		gnafbar_Naf = gna_default*0.2 
		
		insert kdr 
		gkdrbar_kdr=gkdrbar_default*0.001   
		
		gh_soma=soma_hbar
		gh_end=soma_hbar*10
		H_dhalf=300
		H_steep=50
	     	for (x) {  
	       		xdist = distance(x) 
	       		insert h
	       		gbar_h(x) = gh_soma + (gh_end - gh_soma)/(1.0 + exp((H_dhalf-xdist)/H_steep))
		}
		
		kap_distal_maxfactor=1
		kap_distal_distance=100
		kad_distal_maxfactor=0.1
		kad_distal_distance=300  
		//for cortical cell, data is up to 400um
		for (x) {  
	       		xdist=distance(x)
	       		fr1= kad_distal_distance/(xdist+ 00000.1 ) //Divisino by zero?
	       		fr2=xdist/kad_distal_distance
	       		insert kad
	       		insert Ks
	       		if (xdist < kap_distal_distance ) {
	          		gkabar_kad(x) = kad_init*kap_distal_maxfactor
	          		gKsbar_Ks(x) = soma_kdBG*kap_distal_maxfactor  
		       	} else if (xdist < kad_distal_distance ) {
	          		gkabar_kad(x) = kad_distal_maxfactor*kad_init*fr1
	          		gKsbar_Ks(x) = soma_kdBG*kad_distal_maxfactor*fr1     
	       		} else {
	          		gkabar_kad(x) = kad_distal_maxfactor*kad_init
	          		gKsbar_Ks(x) = soma_kdBG*kad_distal_maxfactor  
		       }
	    	}
	      	insert cad       
		insert car	
		insert nap
		insert cal
		insert can
		insert cat
		insert kca
		insert mykca
	 	for (x) {  
	         	xdist = distance(x)
		 	fr = xdist/200
	         	if (xdist < 200) {       
	       			gcabar_car(x) = soma_car*0.5 	
				gnabar_nap(x) = soma_nap
				gcalbar_can(x) = soma_caN/30
	     	    		gcalbar_cal(x) = soma_caL
				gcatbar_cat(x) = soma_caT
				gbar_kca = soma_kca*0.1
				gkbar_mykca = mykca_init*0.05
	         	} else {
				gcabar_car(x) = soma_car*fr  
				gnabar_nap(x) = soma_nap*fr*5
				gcalbar_can(x) = soma_caN*fr*3.2
		   		gcalbar_cal(x) = soma_caL/(30*fr)
				gcatbar_cat(x) = soma_caT*fr  			
				gbar_kca = soma_kca*0.001
				gkbar_mykca = mykca_init *0.001
	         	}
		}
	          	
			                   
		insert ican
	        gbar_ican  = gCAN*0.1
		
		max_ar2=0.95
		min_ar2=0.30
		decay_end=300.0
		decay_start=50.0
	        m_ar2 = (max_ar2 - min_ar2)/(decay_start - decay_end)
	        for (x) {
		                xdist = distance(x)
		                if (xdist < decay_start) { 
		                  	ar2_Naf(x) = max_ar2 
		                } else if (xdist > decay_end) {               
		                  	ar2_Naf(x) = min_ar2 
		                } else {               
		               		ar2_Naf(x) = max_ar2 + m_ar2*xdist
		                }
		//ar2_log("linear", "min_ar2*max_ar2*m_ar2*decay_start*decay_end",$o1)
		}
	}

	access soma//was [0]
	distance()

	forsec basal {
		//nseg = 7//20
	       	insert pas 
	       	e_pas          = v_init
		Ra             = Ra_default  
		cm             = Cm_dend
		Rm_sigmoid_basal() 
	
		insert Naf
		gnafbar_Naf = gna_default*0.1
			
		insert kdr
		gkdrbar_kdr=gkdrbar_default*0.09  

	       	insert ican
	       	gbar_ican  = gCAN*0.1
	              
		// Set the Na+ spike attenuation variable (linearly decreasing from soma to 300 um)
		max_ar2=0.95
	        min_ar2=0.30 
	        decay_end=50.0
	        decay_start=20.0
	       	m_ar2 = (max_ar2 - min_ar2)/(decay_start - decay_end)
		for (x) {
		    	xdist = distance(x)
		               	if (xdist < decay_start) { 
		               		ar2_Naf(x) = max_ar2 
		               	} else if (xdist > decay_end) {               
		               		ar2_Naf(x) = min_ar2 
		               	} else {               
		       			ar2_Naf(x) = max_ar2 + m_ar2*xdist
		               	}
		}
		insert h   //no gradient in somatosensory according to schiller
		gbar_h = soma_hbar
	 	insert kad
	       	insert can
	       	insert cad
		insert nap	
		gnabar_nap=soma_nap
		for (x) {  
			xdist = distance(x)
	 		fr = xdist/50
	               	if (xdist < 50) {        
	       			gcalbar_can(x) = soma_caN/30
				gkabar_kad(x) = kad_init*2 
	       		} else {
				gcalbar_can(x) = soma_caN*0.1
				gkabar_kad(x) = kad_init*5  
	       		}
	 	}
	}
  
	forsec somatic { g_pas=1/Rm_default }
	forall {
		if (ismembrane("Naf")) {ena = 55}
		if (ismembrane("k_ion")) {ek=-85}    
		if(ismembrane("ca_ion")) {
		eca = 140
		cai0_ca_ion =  2.4e-6  
		cao = 2
		ion_style("ca_ion",3,2,1,1,1)
		}
	}
	forall {
	       	for (x) {
	       		if (x > 0 && x < 1) {
	               		diam(x)=diam(x)*1.0
	}}}
	
}

//init() 

endtemplate Pcell

objref Pcells[nPcells]

objref FL[56]
  //printf("RS1cell: setting filenames\n")
  //FL[0]=new String("C3_5_axon.hoc") //OVERRIDE FOR CELL #32!!
  FL[0]=new String("0-2c_axon.hoc")
  FL[1]=new String("0-2_axon.hoc")
  FL[2]=new String("0-2a_axon.hoc")
  FL[3]=new String("0-2b_axon.hoc") // the files 0-2b and 0-2a were forgotten in the listing that I found in Maria's script
  FL[4]=new String("30-3a_axon.hoc")
  FL[5]=new String("30-3b_axon.hoc")
  FL[6]=new String("30-3_axon.hoc")
  FL[7]=new String("33-3_axon.hoc")
  FL[8]=new String("35-2_axon.hoc")
  FL[9]=new String("35-3a_axon.hoc")
  FL[10]=new String("35-3_axon.hoc")
  FL[11]=new String("36-4a_axon.hoc")
  FL[12]=new String("36-4b_axon.hoc")
  FL[13]=new String("36-4_axon.hoc")
  FL[14]=new String("37-3_axon.hoc")
  FL[15]=new String("37-4a_axon.hoc")
  FL[16]=new String("37-4_axon.hoc")
  FL[17]=new String("39-4a_axon.hoc")
  FL[18]=new String("39-5_axon.hoc")
  FL[19]=new String("45-3a_axon.hoc")
  FL[20]=new String("45-3_axon.hoc")
  FL[21]=new String("45-4_axon.hoc")
  FL[22]=new String("47-2a_axon.hoc")
  FL[23]=new String("47-2b_axon.hoc")
  FL[24]=new String("47-3-hf_axon.hoc")
  FL[25]=new String("48-3a_axon.hoc")
  FL[26]=new String("48-3b_axon.hoc")
  FL[27]=new String("48-3c_axon.hoc")
  FL[28]=new String("48-3d_axon.hoc")
  FL[29]=new String("48-4_axon.hoc")
  FL[30]=new String("A5-1B2_axon.hoc")
  FL[31]=new String("C3_4_axon.hoc")
  FL[32]=new String("C3_5_axon.hoc")
  FL[33]=new String("C3_6_axon.hoc")
  FL[34]=new String("G3-1B-2_axon.hoc")
  FL[35]=new String("G4_1_axon.hoc")
  FL[36]=new String("G4_2_axon.hoc")
  FL[37]=new String("H2-3_axon.hoc")
  FL[38]=new String("h-2a_axon.hoc")
  FL[39]=new String("h-2b_axon.hoc")
  FL[40]=new String("h-2_axon.hoc")
  FL[41]=new String("j-2_axon.hoc")
  FL[42]=new String("j-3_axon.hoc")
  FL[43]=new String("j-3a_axon.hoc")
  FL[44]=new String("J3_axon.hoc")
  FL[45]=new String("L-2a_axon.hoc")
  FL[46]=new String("L-2b_axon.hoc")
  FL[47]=new String("L-2_axon.hoc")
  FL[48]=new String("n-3a_axon.hoc")
  FL[49]=new String("n-3b_axon.hoc")
  FL[50]=new String("n-3_axon.hoc")
  FL[51]=new String("q-3a_axon.hoc")
  FL[52]=new String("q-3b_axon.hoc")
  FL[53]=new String("q-3_axon.hoc")
  FL[54]=new String("q-4a_axon.hoc")
  FL[55]=new String("q-4_axon.hoc")

proc createPyrs(){

  strdef filename,basename
  basename="../../morphology/smith_new_axon/"
  printf("initializing cells...\n")

  for i = 0, (nPcells-1) {
	//printf("Picking Morphology #%d...\n",int(PcellIDlist.x[i]))
	cc = 32//int(PcellIDlist.x[i])//PYID//32 is our cell!
      sprint(filename,"%s%s",basename,FL[num2ind(cc)].s)
      Pcells[i] = new Pcell(cc,filename)
  }

}

createPyrs()





printf("Applying current balance\n")
current_balance(v_init)

//Create a list with segments for all neurons
objref soma_list, basal_list, apical_list, dend_list, pcell_list, cell0_list

soma_list = new SectionList()
basal_list = new SectionList()
apical_list = new SectionList()
dend_list = new SectionList()
pcell_list = new SectionList()
for(i=0;i<nPcells;i=i+1) {
  Pcells[i].soma soma_list.append()

  forsec Pcells[i].basal { 
    basal_list.append()
  }

  forsec Pcells[i].apical {
    apical_list.append()
  }

  forsec Pcells[i].basal {
    dend_list.append()
  }

  forsec Pcells[i].apical {
    dend_list.append()
  }

  forsec Pcells[i].all {
    pcell_list.append()
  }
}
/*
cell0_list=new SectionList()
Pcells[0].soma cell0_list.append()
forsec Pcells[0].basal {
	cell0_list.append()
	}
forsec Pcells[0].apical {	
	cell0_list.append()
}*/
