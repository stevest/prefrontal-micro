//Interneuron for PFC 
//Based on Durstewitz and Gabriel 2006
//"Irregular spiking in NMDA-driven prefrontal cortex neurons"

//Template for the interneurons:
begintemplate PVcell
external PARALLEL

public position, x, y, z
public soma, axon, dend
create soma, axon, dend

proc position() { local i // adapted from Migliore 2010
  soma for i = 0, n3d()-1 {
    pt3dchange(i, $1-x+x3d(i), $2-y+y3d(i), $3-z+z3d(i), diam3d(i))
  }
  x = $1  y = $2  z = $3
}

proc init() {
x = y = z = 0 // only change via position
v_init_pv = -73

if(PARALLEL){gid=$1}

create soma, axon, dend
soma_nafin=0.045  
soma_kdrin=0.018  
soma_Kslowin=0.000725*0.1
soma_hin=0.00001
soma_kapin=0.0032*15  
soma_canin=0.0003
soma_kctin=0.0001

soma {
	nseg=1
	L=27
	diam=29//SSS FIX IR! and FF!
	v = v_init_pv

	insert pas
	cm=1.2            //microF/cm2
	g_pas =1/10000    //mho/cm2   
	e_pas = v_initin   //(Kawaguchi k Kubota, 1993 --> -73+-3.9)
	v_initin= -73
	Ra=150 

     insert Nafx
     gnafbar_Nafx= soma_nafin

     insert kdrin
     gkdrbar_kdrin= soma_kdrin

     insert IKs	
     gKsbar_IKs= soma_Kslowin

     insert hin
     gbar_hin=soma_hin

     insert kap
     gkabar_kap=soma_kapin

     insert can
     gcalbar_can=soma_canin

     insert iC 
     gkcbar_iC=soma_kctin

     insert cadynin

}

axon {
	nseg=1
	L=115
	diam=1.5
	v = v_init_pv
	
	insert pas
	cm=1.2            //microF/cm2
	g_pas =1/10000  //mho/cm2   
	e_pas = v_initin
	v_initin= -73
	Ra=150 

	insert Nafx
	gnafbar_Nafx=soma_nafin*10 

	insert kdrin
	gkdrbar_kdrin=soma_kdrin*0.5
}


dend {
	nseg=1
	L=22
	diam=7
	v = v_init_pv

	insert pas
	cm=1.2            //microF/cm2
	g_pas =1/10000    //mho/cm2   
	e_pas = v_initin//(Kawaguchi k Kubota, 1993 --> -73+-3.9)
	v_initin= -73
	Ra=150

	insert Nafx
	gnafbar_Nafx=0.018*5

	insert kdrin
	gkdrbar_kdrin=0.018*0.5

     	insert kap
    	gkabar_kap=soma_kapin*10

}

	ko0_k_ion = 3.82   //mM
 	ki0_k_ion = 140    //mM
	celsius   = 23
	connect axon(0), soma(0.5)
	connect dend(0), soma(0.5)  
}

endtemplate PVcell


//Creating new PV interneurons
objref PVcells, PVgids
PVcells = new List() 
PVgids=new Vector()

proc createPVs(){local i,host,gid localobj cell,mynil
	if(PARALLEL){
		{PC.barrier()}
		  if(PC.id==0) { printf("initializing %d PVs...\n",nPVcells) }
		    for i=0,(nPVcells-1){ //(i=PC.id;i<nPcells;i+=PC.nhost) { 
		      host=i%PC.nhost
		      gid=ind2gid(i,1)
		      if(host==PC.id){
			 PVgids.append(gid)  
			cell = new PVcell(gid)
			//cell.position((i+.5)*len/nPVcells, -100, 0)  // equal spacing of interneurons. Ignores clustering effects     
			PC.set_gid2node(gid,PC.id)
			cell.axon {//synapse_source(gid)//,&v(1))
			  PC.cell(gid,new NetCon(&v(1), mynil),1)
			}        
			PC.threshold(gid,-20)          
			PVcells.append(cell)        
		      }    
		  }

	}else{
		for i = 0, nPVcells-1 {
			cell = new PVcell()
			PVcells.append(cell)
			PVgids.append(i)
		}
	}
}

createPVs()

//Create list with segments
objref PVsoma_list
PVsoma_list = new SectionList()


for i=0,PVcells.count()-1 {		
	PVcells.o(i).soma PVsoma_list.append() 
}

